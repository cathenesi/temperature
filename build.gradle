buildscript {
    ext {
        springBootVersion = '2.0.5.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2"
    }

}

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'eclipse'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'docker'
apply plugin: 'org.sonarqube'

group = 'org.asofth.iot.temperature'
version = '1.0.0'
sourceCompatibility = 1.8

repositories {
    mavenCentral()
	maven { url "https://repo.spring.io/snapshot" }
	maven { url "https://repo.spring.io/milestone" }
}

jacoco{
    toolVersion = '0.8.1'
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.5
            }
        }
    }
}


jacocoTestReport {
    reports {
        csv.enabled false
        xml.enabled true
    }
    executionData(test)
}

tasks.build.dependsOn(jacocoTestReport)

test{
    jacoco{
        append = false
        destinationFile = file("$buildDir/jacoco/jacocoTest.exec")
        classDumpDir = file("$buildDir/jacoco/classpathdumps")
    }
}

ext {
	springCloudVersion = 'Finchley.BUILD-SNAPSHOT'
}

dependencies {
    implementation('io.springfox:springfox-swagger2:2.9.2')
    implementation('io.springfox:springfox-swagger-ui:2.9.2')
    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-websocket')
    implementation('org.springframework.boot:spring-boot-starter-thymeleaf')
    implementation('org.codehaus.jackson:jackson-mapper-asl:1.9.13')
    implementation('io.lettuce:lettuce-core:5.1.0.RELEASE')
    compile('org.springframework.data:spring-data-redis:2.1.0.RELEASE')
    compileOnly('org.projectlombok:lombok:1.16.18')
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    testImplementation('org.mockito:mockito-core:2.23.4')
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

docker {
    baseImage "openjdk:8u171-jdk-alpine3.8"
}

task dockerImage(type: Docker, dependsOn: 'build') {
    group 'build'
    applicationName = "${rootProject.name}".toLowerCase()
    exposePort 9052
    addFile("build/libs/${rootProject.name}-${version}.jar","/tmp")
    workingDir("/tmp")
    entryPoint(["java","-jar","-Dspring.profiles.active=prod","${rootProject.name}-${version}.jar"])
}
